/-
Copyright (c) 2024 Damien Thomine. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Damien Thomine, Pietro Monticone
-/
import Mathlib.Dynamics.TopologicalEntropy.NetEntropy

/-!
# Topological entropy of subsets: monotonicity, closure
Proof that the topological entropy depends monotonically on the subset. Main results
are `entropy_monotone_space‚ÇÅ`/`entropy'_monotone_space‚ÇÅ` (for the cover version)
and `entropy_monotone_space‚ÇÇ`/`entropy'_monotone_space‚ÇÇ` (for the net version). I have decided
to keep all the intermediate steps, since they may be useful in the study of other systems.

For uniformly continuous maps, proof that the entropy of a subset is the entropy of its closure.
Main results are `entropy_of_closure` and `entropy'_of_closure`.

TODO: I think one could implement a notion of Hausdorff onvergence for subsets using uniform
spaces, and then prove the semicontinuity of the topological entropy. It would be a nice
generalization of these lemmas on closures.
-/

namespace Entropy

open CoverEntropy EReal ENNReal NetEntropy Set

variable {X : Type*}

/-! ### Monotonicity of entropy as a function of the subset -/

theorem dyncover_monotone_space {T : X ‚Üí X} {F G : Set X} (F_G : F ‚äÜ G) {U : Set (X √ó X)} {n : ‚Ñï}
    {s : Set X} (h : IsDynamicalCoverOf T G U n s) :
    IsDynamicalCoverOf T F U n s := Subset.trans F_G h

theorem dynnet_monotone_space {T : X ‚Üí X} {F G : Set X} (F_G : F ‚äÜ G ) {U : Set (X √ó X)} {n : ‚Ñï}
    {s : Set X} (h : IsDynamicalNetOf T F U n s) :
    IsDynamicalNetOf T G U n s := ‚ü®Subset.trans h.1 F_G, h.2‚ü©

theorem mincard_monotone_space (T : X ‚Üí X) (U : Set (X √ó X)) (n : ‚Ñï) :
    Monotone (fun F : Set X ‚Ü¶ Mincard T F U n) := by
  intro F G F_G
  simp only [mincard_eq_sInf T F U n, mincard_eq_sInf T G U n]
  exact sInf_le_sInf (image_mono (image_mono fun _ ‚Ü¶ dyncover_monotone_space F_G))

theorem maxcard_monotone_space (T : X ‚Üí X) (U : Set (X √ó X)) (n : ‚Ñï) :
    Monotone (fun F : Set X ‚Ü¶ Maxcard T F U n) := by
  intro F G F_G
  simp only [maxcard_eq_sSup T F U n, maxcard_eq_sSup T G U n]
  exact sSup_le_sSup (image_mono (image_mono (fun _ ‚Ü¶ dynnet_monotone_space F_G)))

theorem CoverEntropyInfUni_monotone_space (T : X ‚Üí X) (U : Set (X √ó X)) :
    Monotone (fun F : Set X ‚Ü¶ CoverEntropyInfUni T F U) := by
  intro F G F_G
  refine liminf_le_liminf <| Filter.eventually_of_forall fun n ‚Ü¶ ?_
  apply EReal.monotone_div_right_of_nonneg (Nat.cast_nonneg' n) (log_monotone _)
  rw [ENat.toENNReal_le]
  exact mincard_monotone_space T U n F_G

theorem CoverEntropySupUni_monotone_space (T : X ‚Üí X) (U : Set (X √ó X)) :
    Monotone (fun F : Set X ‚Ü¶ CoverEntropySupUni T F U) := by
  intro F G F_G
  refine limsup_le_limsup <| Filter.eventually_of_forall fun n ‚Ü¶ ?_
  apply EReal.monotone_div_right_of_nonneg (Nat.cast_nonneg' n) (log_monotone _)
  rw [ENat.toENNReal_le]
  exact mincard_monotone_space T U n F_G

theorem NetEntropyInfUni_monotone_space (T : X ‚Üí X) (U : Set (X √ó X)) :
    Monotone (fun F : Set X ‚Ü¶ NetEntropyInfUni T F U) := by
  intro F G F_G
  refine liminf_le_liminf <| Filter.eventually_of_forall fun n ‚Ü¶ ?_
  apply EReal.monotone_div_right_of_nonneg (Nat.cast_nonneg' n) (log_monotone _)
  rw [ENat.toENNReal_le]
  exact maxcard_monotone_space T U n F_G

theorem NetEntropySupUni_monotone_space (T : X ‚Üí X) (U : Set (X √ó X)) :
    Monotone (fun F : Set X ‚Ü¶ NetEntropySupUni T F U) := by
  intro F G F_G
  refine limsup_le_limsup <| Filter.eventually_of_forall fun n ‚Ü¶ ?_
  apply EReal.monotone_div_right_of_nonneg (Nat.cast_nonneg' n) (log_monotone _)
  rw [ENat.toENNReal_le]
  exact maxcard_monotone_space T U n F_G

theorem CoverEntropyInf_monotone_space [UniformSpace X] (T : X ‚Üí X) :
    Monotone (fun F : Set X ‚Ü¶ CoverEntropyInf T F) :=
  fun _ _ F_G ‚Ü¶ iSup‚ÇÇ_mono (fun U _ ‚Ü¶ CoverEntropyInfUni_monotone_space T U F_G)

theorem CoverEntropySup_monotone_space [UniformSpace X] (T : X ‚Üí X) :
    Monotone (fun F : Set X ‚Ü¶ CoverEntropySup T F) :=
  fun _ _ F_G ‚Ü¶ iSup‚ÇÇ_mono (fun U _ ‚Ü¶ CoverEntropySupUni_monotone_space T U F_G)

theorem NetEntropyInf_monotone_space [UniformSpace X] (T : X ‚Üí X) :
    Monotone (fun F : Set X ‚Ü¶ NetEntropyInf T F) :=
  fun _ _ F_G ‚Ü¶ iSup‚ÇÇ_mono (fun U _ ‚Ü¶ NetEntropyInfUni_monotone_space T U F_G)

theorem NetEntropySup_monotone_space [UniformSpace X] (T : X ‚Üí X) :
    Monotone (fun F : Set X ‚Ü¶ NetEntropySup T F) :=
  fun _ _ F_G ‚Ü¶ iSup‚ÇÇ_mono (fun U _ ‚Ü¶ NetEntropySupUni_monotone_space T U F_G)

/-! ### Topological entropy of closure -/

open DynamicalUniformity Function Uniformity UniformSpace

theorem dyncover_of_closure [UniformSpace X] {T : X ‚Üí X} (h : UniformContinuous T) {F : Set X}
    {U V : Set (X √ó X)} (V_uni : V ‚àà ùì§ X) {n : ‚Ñï} {s : Set X}
    (s_cover : IsDynamicalCoverOf T F U n s) :
    IsDynamicalCoverOf T (closure F) (U ‚óã V) n s := by
  rcases (hasBasis_symmetric.mem_iff' V).1 V_uni with ‚ü®W, ‚ü®W_uni, W_symm‚ü©, W_V‚ü©
  rw [id_eq] at W_V
  refine dyncover_antitone_uni (compRel_mono (Subset.refl U) W_V) (fun x x_clos ‚Ü¶ ?_)
  rcases mem_closure_iff_ball.1 x_clos (dynamical_uni_of_uni h W_uni n) with ‚ü®y, y_x, y_F‚ü©
  specialize s_cover y_F
  simp only [iUnion_coe_set, mem_iUnion, exists_prop] at s_cover
  rcases s_cover with ‚ü®z, z_s, y_z‚ü©
  simp only [iUnion_coe_set, mem_iUnion, exists_prop]
  use z, z_s
  rw [mem_ball_symmetry (dynamical_uni_of_symm T W_symm n)] at y_x
  exact ball_mono (dynamical_uni_of_comp T U W n) z (mem_ball_comp y_z y_x)

theorem mincard_of_closure [UniformSpace X] {T : X ‚Üí X} (h : UniformContinuous T) (F : Set X)
    (U : Set (X √ó X)) {V : Set (X √ó X)} (V_uni : V ‚àà ùì§ X) (n : ‚Ñï) :
    Mincard T (closure F) (U ‚óã V) n ‚â§ Mincard T F U n := by
  rcases eq_top_or_lt_top (Mincard T F U n) with (h' | h')
  . exact h' ‚ñ∏ le_top
  . rcases (finite_mincard_iff T F U n).1 h' with ‚ü®s, s_cover, s_mincard‚ü©
    exact s_mincard ‚ñ∏ mincard_le_card (dyncover_of_closure h V_uni s_cover)

theorem CoverEntropyInfUni_of_closure [UniformSpace X] {T : X ‚Üí X} (h : UniformContinuous T)
  (F : Set X) (U : Set (X √ó X)) {V : Set (X √ó X)} (V_uni : V ‚àà ùì§ X) :
    CoverEntropyInfUni T (closure F) (U ‚óã V) ‚â§ CoverEntropyInfUni T F U := by
  refine liminf_le_liminf <| Filter.eventually_of_forall (fun n ‚Ü¶ ?_)
  apply EReal.monotone_div_right_of_nonneg (Nat.cast_nonneg' n) (log_monotone _)
  rw [ENat.toENNReal_le]
  exact mincard_of_closure h F U V_uni n

theorem CoverEntropySupUni_of_closure [UniformSpace X] {T : X ‚Üí X} (h : UniformContinuous T)
    (F : Set X) (U : Set (X √ó X)) {V : Set (X √ó X)} (V_uni : V ‚àà ùì§ X) :
    CoverEntropySupUni T (closure F) (U ‚óã V) ‚â§ CoverEntropySupUni T F U := by
  refine limsup_le_limsup <| Filter.eventually_of_forall (fun n ‚Ü¶ ?_)
  apply EReal.monotone_div_right_of_nonneg (Nat.cast_nonneg' n) (log_monotone _)
  rw [ENat.toENNReal_le]
  exact mincard_of_closure h F U V_uni n

theorem CoverEntropyInf_of_closure [UniformSpace X] {T : X ‚Üí X} (h : UniformContinuous T)
    (F : Set X) :
    CoverEntropyInf T (closure F) = CoverEntropyInf T F := by
  refine le_antisymm (iSup‚ÇÇ_le fun U U_uni ‚Ü¶ ?_)
    (CoverEntropyInf_monotone_space T (subset_closure (s := F)))
  rcases comp_mem_uniformity_sets U_uni with ‚ü®V, V_uni, V_U‚ü©
  exact le_iSup‚ÇÇ_of_le V V_uni (le_trans (CoverEntropyInfUni_antitone_uni T (closure F) V_U)
    (CoverEntropyInfUni_of_closure h F V V_uni))

theorem CoverEntropySup_of_closure [UniformSpace X] {T : X ‚Üí X} (h : UniformContinuous T)
    (F : Set X) :
    CoverEntropySup T (closure F) = CoverEntropySup T F := by
  refine le_antisymm (iSup‚ÇÇ_le fun U U_uni ‚Ü¶ ?_)
    (CoverEntropySup_monotone_space T (subset_closure (s := F)))
  rcases comp_mem_uniformity_sets U_uni with ‚ü®V, V_uni, V_U‚ü©
  exact le_iSup‚ÇÇ_of_le V V_uni (le_trans (CoverEntropySupUni_antitone_uni T (closure F) V_U)
    (CoverEntropySupUni_of_closure h F V V_uni))

/-! ### Topological entropy of unions -/

theorem dyncover_of_union {T : X ‚Üí X} {F G : Set X} {U : Set (X √ó X)} {n : ‚Ñï} {s t : Set X}
    (hs : IsDynamicalCoverOf T F U n s) (ht : IsDynamicalCoverOf T G U n t) :
    IsDynamicalCoverOf T (F ‚à™ G) U n (s ‚à™ t) := by
  intro x x_FG
  rcases x_FG with (x_F | x_G)
  . refine mem_of_subset_of_mem (iSup‚ÇÇ_mono' fun y y_s ‚Ü¶ ?_) (hs x_F)
    use y, (mem_union_left t y_s)
  . refine mem_of_subset_of_mem (iSup‚ÇÇ_mono' fun y y_t ‚Ü¶ ?_) (ht x_G)
    use y, (mem_union_right s y_t)

theorem mincard_union_le (T : X ‚Üí X) (F G : Set X) (U : Set (X √ó X)) (n : ‚Ñï) :
    Mincard T (F ‚à™ G) U n ‚â§ Mincard T F U n + Mincard T G U n := by
  classical
  rcases eq_top_or_lt_top (Mincard T F U n) with (hF | hF)
  . rw [hF, top_add]; exact le_top
  rcases eq_top_or_lt_top (Mincard T G U n) with (hG | hG)
  . rw [hG, add_top]; exact le_top
  rcases (finite_mincard_iff T F U n).1 hF with ‚ü®s, s_cover, s_mincard‚ü©
  rcases (finite_mincard_iff T G U n).1 hG with ‚ü®t, t_cover, t_mincard‚ü©
  rw [‚Üê s_mincard, ‚Üê t_mincard]
  have := dyncover_of_union s_cover t_cover
  rw [‚Üê Finset.coe_union s t] at this
  apply le_trans (mincard_le_card this) (le_trans (WithTop.coe_mono (Finset.card_union_le s t)) _)
  norm_cast

theorem CoverEntropySupUni_union (T : X ‚Üí X) (F G : Set X) (U : Set (X √ó X)) :
    CoverEntropySupUni T (F ‚à™ G) U = max (CoverEntropySupUni T F U) (CoverEntropySupUni T G U) := by
  classical
  rcases F.eq_empty_or_nonempty with (rfl | hF)
  ¬∑ simp only [empty_union, CoverEntropySupUni_of_empty, bot_le, max_eq_right]
  apply le_antisymm _ (max_le (CoverEntropySupUni_monotone_space T U subset_union_left)
    (CoverEntropySupUni_monotone_space T U subset_union_right))
  simp only
  have key : ‚àÄ n : ‚Ñï, log (Mincard T (F ‚à™ G) U n) / n
      ‚â§ log (max (Mincard T F U n) (Mincard T G U n)) / n + log (2 : ENNReal) / n := by
    intro n
    have h_logm : 0 ‚â§ log (max (Mincard T F U n) (Mincard T G U n)) := by
      rw [Monotone.map_max log_monotone]
      exact le_trans (log_mincard_nonneg_of_nonempty T hF U n) (le_max_left _ _)
    rw [‚Üê div_right_distrib_of_nonneg (c := n) h_logm (zero_le_log_iff.2 one_le_two)]
    apply div_le_div_right_of_nonneg (Nat.cast_nonneg' n)
    rw [‚Üê log_mul_add]
    apply log_monotone
    norm_cast
    rw [mul_two]
    exact le_trans (mincard_union_le T F G U n) (add_le_add (le_max_left _ _) (le_max_right _ _))
  apply le_trans (limsup_le_limsup (Filter.eventually_of_forall fun n ‚Ü¶ key n))
  have := Filter.Tendsto.limsup_eq <| EReal.tendsto_const_div_atTop_nhds_zero_nat
    (ne_of_gt (bot_lt_log_iff.2 Nat.ofNat_pos)) (ne_of_lt (log_lt_top_iff.2 two_lt_top))
  apply le_trans (limsup_add_le_add_limsup (Or.inr (this ‚ñ∏ EReal.zero_ne_top))
    (Or.inr (this ‚ñ∏ EReal.zero_ne_bot)))
  apply le_of_eq
  unfold CoverEntropySupUni
  rw [this, add_zero, ‚Üê limsup_max]
  congr
  ext n
  rw [Monotone.map_max log_monotone,
    Monotone.map_max (EReal.monotone_div_right_of_nonneg (Nat.cast_nonneg' n))]

theorem CoverEntropySup_union [UniformSpace X] (T : X ‚Üí X) (F G : Set X) :
    CoverEntropySup T (F ‚à™ G) = max (CoverEntropySup T F) (CoverEntropySup T G) := by
  unfold CoverEntropySup
  rw [iSup_subtype', iSup_subtype', iSup_subtype', ‚Üê _root_.sup_eq_max, ‚Üê iSup_sup_eq]
  congr
  ext U_uni
  exact CoverEntropySupUni_union T F G U_uni.1

theorem CoverEntropyInf_union_of_inv [UniformSpace X] (T : X ‚Üí X) {F G : Set X}
    (hF : MapsTo T F F) (hG : MapsTo T G G) :
    CoverEntropyInf T (F ‚à™ G) = max (CoverEntropyInf T F) (CoverEntropyInf T G) := by
  rw [CoverEntropyInf_eq_CoverEntropySup_of_inv T hF,
    CoverEntropyInf_eq_CoverEntropySup_of_inv T hG, ‚Üê CoverEntropySup_union T F G]
  exact CoverEntropyInf_eq_CoverEntropySup_of_inv T (MapsTo.union_union hF hG)

/-! ### Topological entropy of finite unions -/

theorem CoverEntropySup_iUnion_le {Œπ : Type*} [UniformSpace X] (T : X ‚Üí X) (F : Œπ ‚Üí Set X) :
    ‚®Ü i, CoverEntropySup T (F i) ‚â§ CoverEntropySup T (‚ãÉ i, F i) :=
  iSup_le (fun i ‚Ü¶ (CoverEntropySup_monotone_space T (subset_iUnion F i)))

theorem CoverEntropySup_biUnion_le {Œπ : Type*} [UniformSpace X] (s : Set Œπ) (T : X ‚Üí X)
   (F : Œπ ‚Üí Set X) :
    ‚®Ü i ‚àà s, CoverEntropySup T (F i) ‚â§ CoverEntropySup T (‚ãÉ i ‚àà s, F i) :=
  iSup‚ÇÇ_le (fun _ i_s ‚Ü¶ (CoverEntropySup_monotone_space T (subset_biUnion_of_mem i_s)))

theorem CoverEntropyInf_iUnion_le {Œπ : Type*} [UniformSpace X] (T : X ‚Üí X) (F : Œπ ‚Üí Set X) :
    ‚®Ü i, CoverEntropyInf T (F i) ‚â§ CoverEntropyInf T (‚ãÉ i, F i) :=
  iSup_le (fun i ‚Ü¶ (CoverEntropyInf_monotone_space T (subset_iUnion F i)))

theorem CoverEntropyInf_biUnion_le {Œπ : Type*} [UniformSpace X] (s : Set Œπ) (T : X ‚Üí X)
   (F : Œπ ‚Üí Set X) :
    ‚®Ü i ‚àà s, CoverEntropyInf T (F i) ‚â§ CoverEntropyInf T (‚ãÉ i ‚àà s, F i) :=
  iSup‚ÇÇ_le (fun _ i_s ‚Ü¶ (CoverEntropyInf_monotone_space T (subset_biUnion_of_mem i_s)))

theorem CoverEntropySup_finite_iUnion {Œπ : Type*} [UniformSpace X] [Fintype Œπ] (T : X ‚Üí X)
    (F : Œπ ‚Üí Set X) :
    CoverEntropySup T (‚ãÉ i, F i) = ‚®Ü i, CoverEntropySup T (F i) := by
  apply Fintype.induction_empty_option (P := fun Œπ _ ‚Ü¶ ‚àÄ F : Œπ ‚Üí Set X,
    CoverEntropySup T (‚ãÉ i, F i) = ‚®Ü i, CoverEntropySup T (F i))
  ¬∑ intro Œ± Œ≤ _ Œ±_Œ≤ h F
    specialize h (F ‚àò Œ±_Œ≤)
    simp only [comp_apply] at h
    rw [‚Üê iUnion_congr_of_surjective (g := F) Œ±_Œ≤ (Equiv.surjective Œ±_Œ≤) (fun _ ‚Ü¶ comp_apply), h]
    exact Equiv.iSup_comp (g := fun i : Œ≤ ‚Ü¶ CoverEntropySup T (F i)) Œ±_Œ≤
  ¬∑ intro _
    simp only [iUnion_of_empty, CoverEntropySup_of_empty, ciSup_of_empty]
  ¬∑ intro Œ± _ h F
    rw [iSup_option, iUnion_option, CoverEntropySup_union T (F none) (‚ãÉ i, F (some i)),
      _root_.sup_eq_max]
    congr
    exact h (fun i : Œ± ‚Ü¶ F (some i))

theorem CoverEntropySup_finite_biUnion {Œπ : Type*} [UniformSpace X] (s : Finset Œπ) (T : X ‚Üí X)
    (F : Œπ ‚Üí Set X) :
    CoverEntropySup T (‚ãÉ i ‚àà s, F i) = ‚®Ü i ‚àà s, CoverEntropySup T (F i) := by
  have fin_coe : Fintype { i // i ‚àà s } := FinsetCoe.fintype s
  have := @CoverEntropySup_finite_iUnion X {i // i ‚àà s} _ fin_coe T (fun i ‚Ü¶ F i)
  rw [iSup_subtype', ‚Üê this, iUnion_subtype]

theorem CoverEntropyInf_finite_iUnion_of_inv {Œπ : Type*} [UniformSpace X] [Fintype Œπ] {T : X ‚Üí X}
    {F : Œπ ‚Üí Set X} (h : ‚àÄ i, MapsTo T (F i) (F i)) :
    CoverEntropyInf T (‚ãÉ i, F i) = ‚®Ü i, CoverEntropyInf T (F i) := by
  rw [CoverEntropyInf_eq_CoverEntropySup_of_inv T (mapsTo_iUnion_iUnion h),
    CoverEntropySup_finite_iUnion T F]
  exact iSup_congr (fun i ‚Ü¶ Eq.symm (CoverEntropyInf_eq_CoverEntropySup_of_inv T (h i)))

theorem CoverEntropyInf_finite_biUnion_of_inv {Œπ : Type*} [UniformSpace X] (s : Finset Œπ)
    (T : X ‚Üí X) {F : Œπ ‚Üí Set X} (h : ‚àÄ i ‚àà s, MapsTo T (F i) (F i)) :
    CoverEntropyInf T (‚ãÉ i ‚àà s, F i) = ‚®Ü i ‚àà s, CoverEntropyInf T (F i) := by
  rw [CoverEntropyInf_eq_CoverEntropySup_of_inv T (mapsTo_iUnion‚ÇÇ_iUnion‚ÇÇ h),
    CoverEntropySup_finite_biUnion s T F]
  exact biSup_congr' (fun i i_s ‚Ü¶ Eq.symm (CoverEntropyInf_eq_CoverEntropySup_of_inv T (h i i_s)))

end Entropy

#lint
