/-
Copyright (c) 2024 Etienne Marion. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Etienne Marion
-/
import Mathlib.Topology.Instances.ENat

/-!
# Compactified sequence

In this file we define `compactSequence f a`. Given `f : â„• â†’ Î±` and `a : Î±`,
`compactSequence f a : â„•âˆ â†’ Î±` is the extension of `f` which sends `âŠ¤` to `a`. This function
is continuous if and only if `f` tends to `a`. This is useful to prove that a `SequentialSpace`
is a `CompactlyGeneratedSpace`.

## Main definitions
* `compactSequence f a`: Given `f : â„• â†’ Î±` and `a : Î±`, `compactSequence f a : â„•âˆ â†’ Î±`
  is the extension of `f` which sends `âŠ¤` to `a`.

## Main statements
* `continuous_compactSequence_iff`: `compactSequence f a` is continuous if and only if
  `f` tends to `a`.
-/

open Set Filter Topology

variable {Î± : Type*} {f : â„• â†’ Î±} {a : Î±}

/-- Given a sequence `f : â„• â†’ Î±` and `a : Î±`, `compactSequence f a : â„•âˆ â†’ Î±` corresponds
to the extension of `f` sending `âŠ¤` to `a`. If `f` tendsto `a` then
`compactSequence f a` is continuous (see `continuous_compactSequence_iff`)
and therefore has compact range (`isCompact_range_compactSequence`). -/
def compactSequence (f : â„• â†’ Î±) (a : Î±) : â„•âˆ â†’ Î± := fun n â†¦
  match n with
  | some k => f k
  | none => a

theorem compactSequence_coe (n : â„•) : compactSequence f a n = f n := rfl

theorem compactSequence_ne_top {n : â„•âˆ} (hn : n â‰  âŠ¤) : compactSequence f a n = f n.toNat := by
  lift n to â„• using hn
  rfl

theorem compactSequence_top : compactSequence f a âŠ¤ = a := rfl

theorem range_compactSequence : range (compactSequence f a) = insert a (range f) := by
  ext b
  constructor
  Â· rintro âŸ¨n, rflâŸ©
    rcases eq_or_ne n âŠ¤ with rfl | hn
    Â· exact mem_insert _ _
    Â· exact mem_insert_of_mem _ âŸ¨n.toNat, (compactSequence_ne_top hn).symmâŸ©
  Â· rw [mem_insert_iff]
    rintro (rfl | âŸ¨n, rflâŸ©)
    Â· exact âŸ¨âŠ¤, rflâŸ©
    Â· exact âŸ¨n, rflâŸ©

variable [TopologicalSpace Î±]

theorem continuous_compactSequence_iff :
    Continuous (compactSequence f a) â†” Tendsto f atTop (ğ“ a) where
  mp h := by
    refine tendsto_atTop_nhds.2 fun U ha hU â†¦ ?_
    have := tendsto_nhds.1 (h.tendsto' âŠ¤ a compactSequence_top) U hU ha
    rcases (nhds_top_basis.mem_iff' _).1 this with âŸ¨N, hN, h'âŸ©
    lift N to â„• using hN.ne
    refine âŸ¨N + 1, fun n hn â†¦ ?_âŸ©
    rw [â† @compactSequence_coe _ f a]
    apply h'
    simp only [mem_Ioi, Nat.cast_lt]
    omega
  mpr h := by
    refine continuous_def.2 fun s hs â†¦ ?_
    by_cases htop : âŠ¤ âˆˆ (compactSequence f a â»Â¹' s)
    Â· rw [isOpen_iff_top_mem _ htop]
      rcases tendsto_atTop_nhds.1 h s htop hs with âŸ¨N, hNâŸ©
      refine âŸ¨N, fun y hy â†¦ ?_âŸ©
      rcases eq_or_ne y âŠ¤ with rfl | y_ne_top
      Â· exact htop
      Â· lift y to â„• using y_ne_top
        exact hN _ (by simpa using hy : N < y).le
    Â· exact isOpen_top_not_mem _ htop

theorem isCompact_range_compactSequence (h : Tendsto f atTop (ğ“ a)) :
    IsCompact (range (compactSequence f a)) :=
  isCompact_range (continuous_compactSequence_iff.2 h)
