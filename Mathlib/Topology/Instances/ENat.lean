/-
Copyright (c) 2024 Etienne Marion. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Etienne Marion
-/
import Mathlib.Data.ENat.Lattice
import Mathlib.Topology.Algebra.Order.Compact

/-!
# Topology on extended natural numbers

In this file we define on topology on `â„•âˆ`, which is the order toplogy. It corresponds to the
one-point compactification of `â„•`. Compactness and Hausdorff are inferred by typeclass inference.

We prove that any set not containing `âŠ¤` is open, and that a set containing `âŠ¤` is open
if and only if it contains `Ioi â†‘n`, for `n : â„•`.

We then use this topology to define `compactSequence f a`. Given `f : â„• â†’ Î±` and `a : Î±`,
`compactSequence f a : â„•âˆ â†’ Î±` is the extension of `f` which sends `âŠ¤` to `a`. This function
is continuous if and only if `f` tends to `a`. This is useful to prove that a `SequentialSpace`
is a `CompactlyGeneratedSpace`.

## Main definitions
* `compactSequence f a`: Given `f : â„• â†’ Î±` and `a : Î±`, `compactSequence f a : â„•âˆ â†’ Î±`
  is the extension of `f` which sends `âŠ¤` to `a`.

## Main statements
* `continuous_compactSequence_iff`: `compactSequence f a` is continuous if and only if
  `f` tends to `a`.
-/

open ENat Filter Topology TopologicalSpace Set

instance : TopologicalSpace â„•âˆ := Preorder.topology â„•âˆ

instance : OrderTopology â„•âˆ := OrderTopology.mk rfl

theorem isOpen_singleton_coe (n : â„•) : IsOpen {(n : â„•âˆ)} := by
  cases n with
  | zero =>
    refine GenerateOpen.basic _ âŸ¨1, Or.inr ?_âŸ©
    ext i
    simp only [ENat.coe_zero, Set.mem_singleton_iff, Set.mem_setOf_eq]
    exact lt_one_iff_eq_zero.symm
  | succ k =>
    have : {(â†‘(k + 1) : â„•âˆ)} = (Set.Iio â†‘(k + 2)) âˆ© (Set.Ioi â†‘k) := by
      rw [Iio_inter_Ioi]
      ext i
      simp only [mem_singleton_iff, mem_Ioo]
      rcases eq_or_ne i âŠ¤ with h | h
      Â· simp only [h, not_top_lt, and_false, iff_false]
        exact top_ne_coe _
      Â· lift i to â„• using h
        norm_cast
        omega
    rw [this]
    apply GenerateOpen.inter <;> constructor
    Â· exact âŸ¨(â†‘(k + 2) : â„•âˆ), Or.inr rflâŸ©
    Â· exact âŸ¨k, Or.inl rflâŸ©

theorem isOpen_singleton_ne_top {n : â„•âˆ} (hn : n â‰  âŠ¤) : IsOpen {n} := by
  lift n to â„• using hn
  exact isOpen_singleton_coe _

theorem isOpen_top_not_mem (s : Set â„•âˆ) (h : âŠ¤ âˆ‰ s) : IsOpen s := by
  rw [â† Set.biUnion_of_singleton s]
  exact isOpen_biUnion fun x hx â†¦ isOpen_singleton_ne_top <| ne_of_mem_of_not_mem hx h

theorem isOpen_iff_top_mem (s : Set â„•âˆ) (top_mem : âŠ¤ âˆˆ s) :
    IsOpen s â†” âˆƒ n : â„•, Set.Ioi â†‘n âŠ† s where
  mp hs := by
    rcases (nhds_top_basis.mem_iff' s).1 (hs.mem_nhds top_mem) with âŸ¨n, n_lt, hnâŸ©
    lift n to â„• using n_lt.ne
    exact âŸ¨n, hnâŸ©
  mpr := by
    rintro âŸ¨a, haâŸ©
    rw [â† Set.inter_union_compl s (Set.Ioi a)]
    apply IsOpen.union
    Â· rw [Set.inter_eq_self_of_subset_right ha]
      exact GenerateOpen.basic _ âŸ¨a, Or.inl rflâŸ©
    Â· apply isOpen_top_not_mem
      simp [top_mem]

theorem ENat.tendsto_coe_atTop :
    Tendsto (@Nat.cast â„•âˆ _) atTop (ğ“ âŠ¤) := by
  rw [tendsto_atTop_nhds]
  intro U mem_U hU
  rcases (isOpen_iff_top_mem _ mem_U).1 hU with âŸ¨N, hUâŸ©
  refine âŸ¨N + 1, fun n hn â†¦ hU ?_âŸ©
  simp only [Set.mem_Ioi, Nat.cast_lt]
  omega
