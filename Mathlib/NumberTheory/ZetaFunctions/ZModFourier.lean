import Mathlib.Analysis.SpecialFunctions.Complex.Circle
import Mathlib.Analysis.Fourier.FourierTransform

open scoped Real

/-- The `AddMonoidHom` from `ZMod N` to `â„ / â„¤` sending `j mod N` to `j / N mod 1`. -/
noncomputable def ZMod.toAddCircle {N : â„•+} : ZMod N â†’+ UnitAddCircle :=
  ZMod.lift N âŸ¨AddMonoidHom.mk' (fun j â†¦ â†‘(j / N : â„)) (by simp [add_div]), by simpâŸ©

lemma ZMod.toAddCircle_coe {N : â„•+} (j : â„¤) :
    ZMod.toAddCircle (j : ZMod N) = â†‘(j / N : â„) := by
  simp [toAddCircle]

lemma ZMod.toAddCircle_apply {N : â„•+} (j : ZMod N) :
    ZMod.toAddCircle j = â†‘(j.val / N : â„) := by
  conv_lhs => rw [show j = (val j : â„¤) by simp, ZMod.toAddCircle_coe]

lemma ZMod.toAddCircle_injective (N : â„•+) : Function.Injective <| toAddCircle (N := N) := by
  intro x y hxy
  have : (0 : â„) < N := Nat.cast_pos.mpr N.pos
  rw [toAddCircle_apply, toAddCircle_apply, AddCircle.coe_eq_coe_iff_of_mem_Ico
      (hp := Real.fact_zero_lt_one) (a := 0), div_left_inj' this.ne', Nat.cast_inj] at hxy
  Â· exact ZMod.val_injective N hxy
  all_goals
  Â· refine âŸ¨by positivity, ?_âŸ©
    rw [zero_add, div_lt_one this, Nat.cast_lt]
    exact ZMod.val_lt _

/-- The additive character from `ZMod N` to the unit circle in `â„‚`, sending `j mod N` to
`exp (2 * Ï€ * I * j / N)`. -/
noncomputable def ZMod.toCircle {N : â„•+} : AddChar (ZMod N) circle where
  toFun := fun j â†¦ (ZMod.toAddCircle j).toCircle
  map_add_mul' a b := by simp_rw [map_add, AddCircle.toCircle_add]
  map_zero_one' := by simp_rw [map_zero, AddCircle.toCircle, â† QuotientAddGroup.mk_zero,
    Function.Periodic.lift_coe, mul_zero, expMapCircle_zero]

lemma ZMod.toCircle_coe {N : â„•+} (j : â„¤) :
    ZMod.toCircle (j : ZMod N) = Complex.exp (2 * Ï€ * Complex.I * j / N) := by
  rw [ZMod.toCircle, AddChar.coe_mk, AddCircle.toCircle, ZMod.toAddCircle_coe,
    Function.Periodic.lift_coe, expMapCircle_apply]
  push_cast
  ring_nf

lemma ZMod.toCircle_apply {N : â„•+} (j : ZMod N) :
    ZMod.toCircle j = Complex.exp (2 * Ï€ * Complex.I * j.val / N) := by
  rw [â† Int.cast_natCast, â† ZMod.toCircle_coe, ZMod.nat_cast_val, ZMod.int_cast_zmod_cast]


section fourier

open BigOperators MeasureTheory

namespace ZMod

/-- Auxiliary lemma to translate integrability statements into summability -/
lemma integrable_count_iff {ð“š G : Type*} [NormedAddCommGroup G]
    [SecondCountableTopology G] {f : ð“š â†’ G} :
    Integrable f (@Measure.count _ âŠ¤) â†” Summable (fun k â†¦ â€–f kâ€–) := by
  letI : MeasurableSpace G := borel G
  haveI : BorelSpace G := âŸ¨rflâŸ©
  letI : MeasurableSpace ð“š := âŠ¤
  simp_rw [Integrable, eq_true_intro (by measurability : AEStronglyMeasurable f Measure.count),
    true_and, HasFiniteIntegral, lintegral_count, lt_top_iff_ne_top,
    ENNReal.tsum_coe_ne_top_iff_summable, â† NNReal.summable_coe, coe_nnnorm]

lemma Finite.summable {Î± M : Type*} [Finite Î±] [AddCommMonoid M] [TopologicalSpace M]
    (f : Î± â†’ M) : Summable f :=
  summable_of_finite_support <| Set.finite_univ.subset (Set.subset_univ _)

/-- The discrete measurable space structure (every set is measurable). -/
local instance instMeasurableSpaceZMod (N : â„•+) : MeasurableSpace (ZMod N) := âŠ¤

/-- The discrete Fourier transform on `â„¤ / Nâ„¤`. -/
noncomputable def discreteFourierTransform {N : â„•+} (Î¦ : ZMod N â†’ â„‚) (k : ZMod N) : â„‚ :=
  Fourier.fourierIntegral ZMod.toCircle Measure.count Î¦ k

@[inherit_doc]
scoped notation "ð“•" => discreteFourierTransform

lemma discreteFourierTransform_def {N : â„•+} (Î¦ : ZMod N â†’ â„‚) (k : ZMod N) :
    discreteFourierTransform Î¦ k = âˆ‘ j : ZMod N, ZMod.toCircle (-(j * k)) â€¢ Î¦ j := by
  rw [discreteFourierTransform, Fourier.fourierIntegral_def,
    integral_countable' (integrable_count_iff.mpr <| Finite.summable _), tsum_fintype]
  congr 1 with j
  simp_rw [Measure.count_singleton, ENNReal.one_toReal, one_smul]

end ZMod

end fourier
