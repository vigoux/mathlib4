/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.Analysis.SpecialFunctions.Complex.Circle
import Mathlib.Analysis.Fourier.FourierTransform
import Mathlib.NumberTheory.DirichletCharacter.GaussSum

/-!
# Fourier theory on `ZMod N`

Basic definitions and properties of the discrete Fourier transform for functions on `ZMod N`.

### Main definitions and results

* `ZMod.dft`: the Fourier transform with respect to the standard additive character
  `ZMod.stdAddChar` (mapping `j mod N` to `exp (2 * Ï€ * I * j / N)`). The notation `ğ“•`, scoped in
  namespace `ZMod`, is available for this.
* `DirichletCharacter.fourierTransform_eq_inv_mul_gaussSum`: the discrete Fourier transform of a
  primitive Dirichlet character `Ï‡` is a Gauss sum times `Ï‡â»Â¹`.
-/

open scoped Real

open MeasureTheory Finset

/-- A function is _even_ if it satisfis `f (-x) = f x` for all `x`. -/
protected def Function.Even {R R' : Type*} [Neg R] (f : R â†’ R') : Prop := âˆ€ (x : R), f (-x) = f x

/-- A function is _odd_ if it satisfis `f (-x) = -f x` for all `x`. -/
protected def Function.Odd {R R' : Type*} [Neg R] [Neg R'] (f : R â†’ R') : Prop :=
  âˆ€ (x : R), f (-x) = -(f x)

namespace ZMod

variable {N : â„•} [NeZero N]

/-- The discrete Fourier transform on `â„¤ / N â„¤` (with the counting measure) -/
noncomputable def dft (Î¦ : ZMod N â†’ â„‚) (k : ZMod N) : â„‚ :=
  Fourier.fourierIntegral toCircle Measure.count Î¦ k

@[inherit_doc] scoped notation "ğ“•" => dft

lemma dft_apply (Î¦ : ZMod N â†’ â„‚) (k : ZMod N) :
    ğ“• Î¦ k = âˆ‘ j : ZMod N, toCircle (-(j * k)) â€¢ Î¦ j := by
  simp only [dft, Fourier.fourierIntegral_def, integral_countable' <| .of_finite ..,
    Measure.count_singleton, ENNReal.one_toReal, one_smul, tsum_fintype]

lemma dft_def (Î¦ : ZMod N â†’ â„‚) : ğ“• Î¦ = fun k â†¦ âˆ‘ j : ZMod N, toCircle (-(j * k)) â€¢ Î¦ j :=
  funext (dft_apply Î¦)

lemma dft_neg (Î¦ : ZMod N â†’ â„‚) : ğ“• (fun j â†¦ -(Î¦ j)) = fun k â†¦ -(ğ“• Î¦ k) := by
  simp only [dft_def, smul_neg, sum_neg_distrib]

/-- Fourier inversion formula, discrete case. -/
lemma dft_dft (Î¦ : ZMod N â†’ â„‚) : ğ“• (ğ“• Î¦) = fun j â†¦ N * Î¦ (-j) := by
  ext1 j
  simp only [dft_def]
  change âˆ‘ k, stdAddChar _ * âˆ‘ l, stdAddChar _ * _ = _
  simp only [mul_sum, â† mul_assoc, â† AddChar.map_add_eq_mul, mul_comm _ j, â† neg_add, â† add_mul]
  rw [sum_comm]
  simp only [â† sum_mul, â† neg_mul]
  have h1 (t : ZMod N) : âˆ‘ i, stdAddChar (t * i) = if t = 0 then â†‘N else 0 := by
    split_ifs with h
    Â· simp only [h, zero_mul, AddChar.map_zero_eq_one, sum_const, card_univ, card,
        nsmul_eq_mul, mul_one]
    Â· exact AddChar.sum_eq_zero_of_ne_one (AddChar.isPrimitive_stdAddChar N h)
  have h2 (x j : ZMod N) : -(j + x) = 0 â†” x = -j := by
    rw [neg_add, add_comm, add_eq_zero_iff_neg_eq, neg_neg]
  simp only [h1, h2, ite_mul, zero_mul, sum_ite_eq', mem_univ, â†“reduceIte]

section signs

lemma dft_comp_neg (Î¦ : ZMod N â†’ â„‚) : ğ“• (fun j â†¦ Î¦ (-j)) = fun k â†¦ ğ“• Î¦ (-k) := by
  ext1 k
  simp only [dft_def]
  exact Fintype.sum_equiv (Equiv.neg _) _ _ fun j â†¦ by rw [Equiv.neg_apply, neg_mul_neg]

/-- The Fourier transform sends even functions to even functions. -/
lemma dft_even {Î¦ : ZMod N â†’ â„‚} : Î¦.Even â†” (ğ“• Î¦).Even := by
  have h {Î¦ : ZMod N â†’ â„‚} (hÎ¦ : Function.Even Î¦) : Function.Even (ğ“• Î¦) := by
    simp only [Function.Even, â† fun y â†¦ congr_fun (dft_comp_neg Î¦) y, funext fun y â†¦ hÎ¦ y,
      implies_true]
  refine âŸ¨h, fun hÎ¦ x â†¦ ?_âŸ©
  simpa only [neg_neg, mul_right_inj' (NeZero.ne (N : â„‚)), dft_dft] using h hÎ¦ (-x)

/-- The Fourier transform sends odd functions to odd functions. -/
lemma dft_odd {Î¦ : ZMod N â†’ â„‚} : Î¦.Odd â†” (ğ“• Î¦).Odd := by
  have h {Î¦ : ZMod N â†’ â„‚} (hÎ¦ : Function.Odd Î¦) : Function.Odd (ğ“• Î¦) := by
    simp only [Function.Odd, â† fun y â†¦ congr_fun (dft_comp_neg Î¦) y, funext fun y â†¦ hÎ¦ y,
      dft_neg, implies_true]
  refine âŸ¨h, fun hÎ¦ x â†¦ ?_âŸ©
  simpa only [neg_neg, dft_dft, â† mul_neg, mul_right_inj' (NeZero.ne (N : â„‚))] using h hÎ¦ (-x)

end signs

end ZMod

open ZMod

namespace DirichletCharacter

variable {N : â„•} [NeZero N] (Ï‡ : DirichletCharacter â„‚ N)

lemma fourierTransform_eq_gaussSum_mulShift (k : ZMod N) :
    ğ“• Ï‡ k = gaussSum Ï‡ (stdAddChar.mulShift (-k)) := by
  simp only [dft_def]
  refine congr_arg Finset.univ.sum (funext fun j â†¦ ?_)
  rw [AddChar.mulShift_apply, mul_comm j, Submonoid.smul_def, smul_eq_mul, neg_mul,
    stdAddChar_apply, mul_comm (Ï‡ _)]

/-- For a primitive Dirichlet character `Ï‡`, the Fourier transform of `Ï‡` is a constant multiple
of `Ï‡â»Â¹` (and the constant is essentially the Gauss sum). -/
lemma fourierTransform_eq_inv_mul_gaussSum (k : ZMod N) (hÏ‡ : IsPrimitive Ï‡) :
    ğ“• Ï‡ k = Ï‡â»Â¹ (-k) * gaussSum Ï‡ stdAddChar := by
  rw [fourierTransform_eq_gaussSum_mulShift, gaussSum_mulShift_of_isPrimitive _ hÏ‡]

end DirichletCharacter
